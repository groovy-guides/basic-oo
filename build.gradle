apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    jcenter()
}

dependencies {
    compile module(group: 'org.codehaus.groovy', name: 'groovy-all', version: "$project.groovyVersion") {
        dependency(group: 'commons-cli', name: 'commons-cli', version: '1.2')
    }
    testCompile group: 'org.spockframework', name: 'spock-core', version: "$project.spockVersion"
}

project.mainClassName = 'com.github.groovyguides.shapes.app.Cli'
project.codenarc.toolVersion = project.codeNarcVersion


task runScript(dependsOn: 'classes', type: JavaExec) {
    description 'Runs the groovy script'
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = project.gradleVersion
}

codenarcMain {
    ignoreFailures false
    configFile file('config/codenarc/codenarc.xml')
    exclude '**/package-info.groovy'
    maxPriority1Violations 0
    maxPriority2Violations 10
    maxPriority3Violations 20
}


codenarcTest {
    ignoreFailures true
    configFile file('config/codenarc/codenarc-test.xml')
    maxPriority1Violations 0
    maxPriority2Violations 10
    maxPriority3Violations 20
}
